class HDArcanumTome : HDWeapon
{
	enum ArcanumTomeFlags
	{
		ATF_InSpellMenu = 1 << 0
	}

	enum ArcanumTomeProperties
	{
		ATProp_Flags,
		ATProp_SelectedTree,
		ATProp_SelectedSpell,
	}
	
	int mana;
	bool endless;
	bool holy;
	bool forbidden;
	int maximumMana;
	int absorberMultiplier;
	
	
	clearscope int GetModifiedRepCost(ArcanumSpell spell)
	{
		int manaCost = spell.GetManaCost();
		if (!arcanum_use_specialized_tags)
			return manaCost;
			
		int spellRep = spell.GetSpellReputation();
		int currentRep = holy ? 0 : forbidden ? 2 : 1;
		
		if (currentRep == 1) // Neutral, use normally
			return manaCost;
		int difference = currentRep - spellRep;
		if (difference == 0)
			return int(double(manaCost) * 0.8);
		if (abs(difference) == 2)
			return int(double(manaCost) * 1.2);
		return manaCost;
	}
	clearscope int GetRealMaxAmount()
	{
		if (Endless)
			return int.MAX;
		return maximumMana;
	}
	
	

	override void DoEffect()
	{
		Super.DoEffect();
		if (!CoreHandler)
		{
			CoreHandler = AceCoreHandler(EventHandler.Find('AceCoreHandler'));
		}
		
		for (int i = 0; i < CoreHandler.Requests.Size(); ++i)
		{
			InterfaceRequest req = CoreHandler.Requests[i];
			if (req.Receiver == self)
			{
				switch (req.RequestName)
				{
					case 'DrainChargeArcanum':
					{
						int amt = req.Arg.ToInt();
						if (amt != 0)
						{
							mana = clamp(mana + amt, 0, GetRealMaxAmount());
						}
						req.Remove(); i--; // [Ace] req.Remove() will shift the index. We need to make sure the next element is not skipped.
						break;
					}
				}
			}
		}
		

		int curBlues = owner.CountInv('HealingMagic');
		bool hasCube = AceCore.CheckForItem(owner, 'HDSoulCube');
		double baseSpeed = HDHM_BALL / (35 * 60 * (hasCube ? 15.0 : 20.0));
		
		
		bool fullNormal = mana >= maximumMana;
		if (curBlues > 0 && GetRealMaxAmount() > mana)
		{
			double speedFac = max(0.5, curBlues / double(HDHM_BALL));
			double one = 1;
			speedFac *= absorberMultiplier + 1;
			speedFac *= arcanum_absorption_multiplier;
			
			if (endless && fullNormal)
			{
				speedFac *= 0.5;
				one = 2;
			}

			if (owner.FindInventory('PowerInvulnerable'))
			{
				speedFac *= 5.0;
			}

			AccumulatedBlues += baseSpeed * speedFac;
			if (AccumulatedBlues >= one)
			{
				int toCharge = int(AccumulatedBlues);
				toCharge /= int(one); // Endless makes it twice as inefficient
				owner.A_TakeInventory('HealingMagic', toCharge);
				if (forbidden)
				{
					toCharge *= arcanum_forbidden_multiplier;
					if (!random(0, arcanum_forbidden_damage_chance / (absorberMultiplier + 1)))
					{
						let selfPlayer = hdplayerpawn(owner);
						selfPlayer.aggravateddamage += random(-1, 3);
						selfPlayer.A_TakeInventory("HealingMagic", 17);
						A_SpawnItemEx("BFGNecroShard",
							frandom(-4,4),frandom(-4,4),frandom(6,24),
							frandom(1,6),0,frandom(1,3),
							frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER
						);
						if (!random(0, 3))
							selfPlayer.cheatgivestatusailments("fire", random(1, 14));
						if (!random(0, 3))
							owner.A_Log("You feel power being pulled from you.", true);
					}
				}
				mana = min(mana + toCharge, GetRealMaxAmount());
				AccumulatedBlues %= 1.0; // [Ace] Don't lose fractions.
			}
		}
		// Divine light fills the book with blues.
		else if (Holy && curBlues == 0 && (mana < arcanum_holy_generate_amount && GetRealMaxAmount() > mana))
		{
			double rootSpeed = baseSpeed * 1.0;
			AccumulatedBlues += rootSpeed * arcanum_holy_generate_speed_mult;
			if (AccumulatedBlues >= 1)
			{
				int toCharge = int(AccumulatedBlues);
				mana = min(mana + toCharge, GetRealMaxAmount());
				AccumulatedBlues %= 1.0;
			}
		}
		else
		{
			AccumulatedBlues = 0;
		}
	}

	override void PreTravelled()
	{
		Super.PreTravelled();

		int amt = min(GetRealMaxAmount() - mana, owner.CountInv('HealingMagic'));
		if (amt > 0)
		{
			mana += amt;
			owner.A_TakeInventory('HealingMagic', amt);
		}
	}

	override bool AddSpareWeapon(actor newowner) { return AddSpareWeaponRegular(newowner); }
	override HDWeapon GetSpareWeapon(actor newowner , bool reverse, bool doselect) { return GetSpareWeaponRegular(newowner, reverse, doselect); }
	override double GunMass() { return 4; }
	override string, double GetPickupSprite() { return "ARCTZ0", 1.0; }
	override string GetHelpText()
	{
		string HText = WEPHELP_FIRE.."  Place rune\n"
		..WEPHELP_ALTFIRE.."  Create spell circle (absorption: "..(absorberMultiplier)..", "..(maximumMana)..")\n"
		..WEPHELP_ZOOM.."  Spell manager\n";
			

		if (WeaponStatus[ATProp_Flags] & ATF_InSpellMenu)
		{
			HText = WEPHELP_FIRE.." or "..WEPHELP_ALTFIRE.."  Cycle spells\n"
			..WEPHELP_FIREMODE.."+"..WEPHELP_FIRE.." or "..WEPHELP_ALTFIRE.."  Cycle spell trees\n"
			..WEPHELP_ZOOM.."  Exit spell manager";
		}

		return HText;
	}

	override void DrawHUDStuff(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl)
	{
		if (!hpl)
		{
			return;
		}
		
		ArcanumSpell selSpell = GetSelectedSpell();
		selSpell.DrawHUDStuff(sb, hpl, self);
		let book = HDArcanumTome(hdw);

		// [Ace] Ponder this mana orb.
		if (sb.hudlevel == 1)
		{
			double fillFac = double(book.mana) / double(book.maximumMana);
			double visualFill = clamp(fillFac, 0.0, 1.0);

			static const int Lengths[] = { 3, 7, 9, 9, 11, 11, 11, 9, 9, 7, 3 };
			for (int i = 0; i < int(11 * visualFill); ++i)
			{
				sb.Fill(0xFF0022FF, -53 - Lengths[i], -5 - i * 2, Lengths[i] * 2, 2, sb.DI_SCREEN_CENTER_BOTTOM);
			}

			int curBlues = hpl.CountInv('HealingMagic');
			if (curBlues > 0)
			{
				// Oh no frag
				if (forbidden)
				{
					sb.Fill(0xFFF08080, -70, -7, 6, 2, sb.DI_SCREEN_CENTER_BOTTOM);
					sb.Fill(0xFFF08080, -68, -9, 2, 6, sb.DI_SCREEN_CENTER_BOTTOM);
				}
				// Normal
				else
				{
					sb.Fill(0xFF0022FF, -70, -7, 6, 2, sb.DI_SCREEN_CENTER_BOTTOM);
					sb.Fill(0xFF0022FF, -68, -9, 2, 6, sb.DI_SCREEN_CENTER_BOTTOM);
				}
			}
			// Passively giving blues
			else if (book.holy && mana < arcanum_holy_generate_amount)
			{
				sb.Fill(0xFFFFFFE0, -70, -7, 6, 2, sb.DI_SCREEN_CENTER_BOTTOM);
				sb.Fill(0xFFFFFFE0, -68, -9, 2, 6, sb.DI_SCREEN_CENTER_BOTTOM);
			}
			// Blue++, reserved only for endless books
			if (fillFac > 1.0)
			{
				sb.Fill(0xFF0022FF, -40, -24, 6, 2, sb.DI_SCREEN_CENTER_BOTTOM);
				sb.Fill(0xFF0022FF, -38, -26, 2, 6, sb.DI_SCREEN_CENTER_BOTTOM);
			}
		}

		Actor ptr = SpiedActor ? SpiedActor : Actor(hpl);
		Array<ArcanumIndicator> indicators;
		int totalIndicators = 0;
		for (int i = 0; i < SpellStorage.Trees.Size(); ++i)
		{
			for (int j = 0; j < SpellStorage.Trees[i].Spells.Size(); ++j)
			{
				string icon; class<Powerup> cls;
				[icon, cls] = SpellStorage.Trees[i].Spells[j].GetIndicatorInfo();
				if (icon != "" && cls && ptr.FindInventory(cls))
				{
					let ind = new('ArcanumIndicator');
					ind.Icon = icon;
					ind.Pos = (-115 - 20 * (totalIndicators % 5), -5 - 20 * (totalIndicators / 5));
					ind.Col = 0xFF000000 | GetDefaultByType(SpellStorage.Trees[i].Spells[j].GetSigil()).fillcolor;
					indicators.Push(ind);
					totalIndicators++;
				}
			}
		}
		for (int i = 0; i < indicators.Size(); ++i)
		{
			sb.DrawImage(indicators[i].Icon, indicators[i].Pos, sb.DI_SCREEN_CENTER_BOTTOM | sb.DI_ITEM_CENTER_BOTTOM, 0.8, (15, 15), col: indicators[i].Col);
		}

		if (!(hdw.WeaponStatus[ATProp_Flags] & ATF_InSpellMenu))
		{
			return;
		}

		if (!TitleFont)
		{
			TitleFont = HudFont.Create("SPTTLFNT");
		}
		if (!DescFont)
		{
			DescFont = HUDFont.Create("SPDSCFNT");
		}
		if (!ExpFont)
		{
			ExpFont = HUDFont.Create("SPDSCFNT", 6, Mono_CellCenter);
		}

		int baseOffset = -100;

		ArcanumSpellTree SelTree = SpellStorage.Trees[hdw.WeaponStatus[ATProp_SelectedTree]];
		sb.DrawString(TitleFont, SelTree.GetName(), (-140, baseOffset), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT);
		sb.DrawString(DescFont, SelTree.GetDescription(), (-140, baseOffset + 20), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT, Font.CR_WHITE, 1.0, 280, 1);

		sb.DrawString(TitleFont, "Spells", (-140, baseOffset + 90), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT);
		double mult = AceCore.CheckForItem(hpl, "HDSoulCube") ? 0.80 : 1.0;
		for (int i = 0; i < SelTree.Spells.Size(); ++i)
		{
			ArcanumSpell CurSpell = SelTree.Spells[i];
			int cost = GetModifiedRepCost(CurSpell);
			bool canCast = (cost * mult) < book.maximumMana;
			sb.DrawString(DescFont, CurSpell.GetName(), (-140, baseOffset + 110 + 10 * i), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT, 
				CurSpell == SelSpell ? Font.CR_GOLD : 
				canCast ? Font.CR_WHITE : 
				book.Endless ? Font.CR_PURPLE : Font.CR_BLACK);
		}
		
		int yOff = baseOffset + 90;
		sb.DrawString(TitleFont, selSpell.GetName(), (-40, yOff), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT, Font.CR_TEAL);
		if (selSpell.GetMaxExperience() > 0)
		{
			sb.DrawString(DescFont, "Experience", (-40, yOff + 20), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT, Font.CR_GOLD);
			string expStr = "[";
			for (int i = 0; i < 16; ++i)
			{
				string notch = " ";
				if (i < floor(16 * selSpell.GetExperienceFactor()))
				{
					notch = "=";
				}
				expStr = expStr..notch;
			}
			expStr = expStr.."]";
			sb.DrawString(ExpFont, expStr, (14, yOff + 20), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT, Font.CR_WHITE);
			yOff += 10;
		}
		
		sb.DrawString(DescFont, selSpell.GetTechnicalInfo(), (-40, yOff + 20), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT, Font.CR_OLIVE);

		int cost = int(GetModifiedRepCost(selSpell) * mult);

		string str = String.Format("\c[ArcanumReqBlue]%i\c-", cost);
		sb.DrawString(DescFont, "\c[DarkGray]Cost: "..str, (-40, yOff + 30), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT);
		sb.DrawString(DescFont, selSpell.GetDescription(), (-40, yOff + 50), sb.DI_SCREEN_CENTER | sb.DI_TEXT_ALIGN_LEFT, Font.CR_WHITE, 1.0, 180, 1);
	}

	override void DrawSightPicture(HDStatusBar sb, HDWeapon hdw, HDPlayerPawn hpl, bool sightbob, vector2 bob, double fov, bool scopeview, actor hpc, string whichdot)
	{
		int cx, cy, cw, ch;
		[cx, cy, cw, ch] = Screen.GetClipRect();
		sb.SetClipRect(-16 + bob.x, -16 + bob.y, 32, 32, sb.DI_SCREEN_CENTER);
		vector2 bob2 = bob * 2;
		bob2.y = clamp(bob2.y, -8, 8);
		sb.DrawImage("ARCNFRNT", (0, -2) + bob2, sb.DI_SCREEN_CENTER | sb.DI_ITEM_TOP, alpha: 0.9);
		sb.SetClipRect(cx, cy, cw, ch);
	}

	override void LoadoutConfigure(string input)
	{
		int StartBlues = GetLoadoutVar(input, "blues", 4);
		BluesToGive = clamp(StartBlues, 0, 9999);
		
		absorberMultiplier = clamp(GetLoadoutVar(input, "absorber", 1), 0, 9);
		maximumMana = (HDHM_BALL - (HDHM_BOTTLE * absorberMultiplier * 1.5)) * arcanum_stored_max_multiplier;
		
		forbidden = GetLoadoutVar(input, "forbidden", 1) > 0;
		holy = GetLoadoutVar(input, "holy", 1) > 0 && !forbidden;
		int percentage = clamp(GetLoadoutVar(input, "filled", 3), 0, 100);
		double filledAmount = double(percentage) / 100.0;
		mana = int(GetRealMaxAmount() * filledAmount);
		endless = GetLoadoutVar(input, "endless", 1) > 0;
	}
	void Initialize(int startingBlues, int _absorbMultiplier, bool _forbidden, bool _holy, int percentage, bool _endless)
	{
		BluesToGive = clamp(startingBlues, 0, 999999);
		
		absorberMultiplier = clamp(_absorbMultiplier, 0, 9);
		maximumMana = (HDHM_BALL - (HDHM_BOTTLE * _absorbMultiplier * 1.5)) * arcanum_stored_max_multiplier;
		
		forbidden = _forbidden;
		holy = _holy && !forbidden;
		int percentage = clamp(percentage, 0, 100);
		double filledAmount = double(percentage) / 100.0;
		mana = int(GetRealMaxAmount() * filledAmount);
		endless = _endless;
	}

	override void DetachFromOwner()
	{
		WeaponStatus[ATProp_Flags] &= ~ATF_InSpellMenu;

		Super.DetachFromOwner();
	}

	override void AttachToOwner(Actor other)
	{
		Super.AttachToOwner(other);

		let plr = HDPlayerPawn(other);
		if (plr && BluesToGive > 0)
		{
			plr.A_GiveInventory('HealingMagic', BluesToGive);
			BluesToGive = 0;
		}

		InitStorage();
	}

	private void InitStorage()
	{
		SpellStorage = ArcanumSpellBrain.Get(owner.PlayerNumber());
	}

	private clearscope ArcanumSpellTree GetSelectedTree()
	{
		return SpellStorage.Trees[WeaponStatus[ATProp_SelectedTree]];
	}

	private clearscope ArcanumSpell GetSelectedSpell()
	{
		return GetSelectedTree().Spells[WeaponStatus[ATProp_SelectedSpell]];
	}

	private action void A_NextTree()
	{
		int TreeCount = invoker.SpellStorage.Trees.Size();
		invoker.WeaponStatus[ATProp_SelectedSpell] = 0;
		if (++invoker.WeaponStatus[ATProp_SelectedTree] >= TreeCount)
		{
			invoker.WeaponStatus[ATProp_SelectedTree] = 0;
		}
	}

	private action void A_PrevTree()
	{
		int TreeCount = invoker.SpellStorage.Trees.Size();
		invoker.WeaponStatus[ATProp_SelectedSpell] = 0;
		if (--invoker.WeaponStatus[ATProp_SelectedTree] < 0)
		{
			invoker.WeaponStatus[ATProp_SelectedTree] = TreeCount - 1;
		}
	}

	private action void A_NextSpell()
	{
		int SpellCount = invoker.SpellStorage.SpellCountInTree(invoker.SpellStorage.Trees[invoker.WeaponStatus[ATProp_SelectedTree]].GetClass());
		invoker.WeaponStatus[ATProp_SelectedSpell]++;
		if (invoker.WeaponStatus[ATProp_SelectedSpell] >= SpellCount)
		{
			invoker.WeaponStatus[ATProp_SelectedSpell] = 0;
		}
	}

	private action void A_PrevSpell()
	{
		int SpellCount = invoker.SpellStorage.SpellCountInTree(invoker.SpellStorage.Trees[invoker.WeaponStatus[ATProp_SelectedTree]].GetClass());
		invoker.WeaponStatus[ATProp_SelectedSpell]--;
		if (invoker.WeaponStatus[ATProp_SelectedSpell] < 0)
		{
			invoker.WeaponStatus[ATProp_SelectedSpell] = SpellCount - 1;
		}
	}

	override void ActualPickup(Actor other, bool silent)
	{
		let tome = HDArcanumTome(other.FindInventory("HDArcanumTome"));
		if (tome && tome.SpellStorage)
		{
			tome.mana = min(tome.mana + mana, GetRealMaxAmount());
			
			// [Ace] Only give experience to spells that fit the bill, i.e. those that actually support experience.
			Array<ArcanumSpell> valid;
			for (int i = 0; i < tome.SpellStorage.Trees.Size(); ++i)
			{
				for (int j = 0; j < tome.SpellStorage.Trees[i].Spells.Size(); ++j)
				{
					let cur = tome.SpellStorage.Trees[i].Spells[j];
					if (cur.Experience < cur.GetMaxExperience())
					{
						valid.Push(cur);
					}
				}
			}

			if (valid.Size() > 0)
			{
				other.A_GiveInventory('HealingMagic', BluesToGive);
				ArcanumSpell randSpell = valid[random(0, valid.Size() - 1)];
				if (randSpell.GainExperience(randSpell.GetMaxExperience() / (multiplayer ? 4.0 : 5.0)))
				{
					other.A_Log("\c[Fire]"..randSpell.GetName().."\c- has reached full power.", true);
				}
				else
				{
					other.A_Log("Your knowledge of \c[Fire]"..randSpell.GetName().."\c- increases.", true);
				}
			}
			else
			{
				other.A_Log("You fail to learn anything new.", true);
			}
			
			other.A_StartSound("weapons/pocket");
			Destroy();
			return;
		}
		
		Super.ActualPickup(other, silent);
	}

	private transient ui HUDFont TitleFont, DescFont, ExpFont;
	private double AccumulatedBlues;
	private int BluesToGive;
	private AceCoreHandler CoreHandler;
	ArcanumSpellBrain SpellStorage;
	private transient CVar InvertSelection;
	private transient CVar ZoomHold;
	Actor SpiedActor;

	Default
	{
		+HDWEAPON.FITSINBACKPACK
		+INVENTORY.INVBAR
		HDWeapon.BarrelSize 5, 2, 1; // [Ace] I don't even know why.
		Scale 0.6;
		Tag "Arcanum tome";
		Inventory.PickupSound "weapons/pocket";
		HDWeapon.Refid "arc";
		Inventory.MaxAmount 1;
		Inventory.PickupMessage "You picked up the Arcanum tome of knowledge.";
		hdweapon.loadoutcodes "
			\cublues - 0/999999, gives you blues based on amount.
			\cufilled - 0/100, how much it was filled already.
			\cuabsorber - 0/9, absorbs your blues faster in turn to hold less overall, split in potion sips.
			\cuforbidden - 0/1, improves blue absorption efficiency at the cost of something else.
			\cuendless - 0/1, allows it to add beyond the normal capacity, less efficient when it does.
			\cuholy - 0/1, gives a sliver of mana when low.";
	}
	override int getsbarnum(int flags)
	{
		return arcanum_show_amount ? mana : super.getsbarnum(flags);
	}
	
	
	override double WeaponBulk()
	{
		double baseBulk = holy ? 60 : forbidden ? 35 : 25;
		baseBulk -= absorberMultiplier;
		if (endless)
		{
			double additionalMana = double(mana) - double(maximumMana);
			baseBulk += 20 + max(40 * (additionalMana / double(maximumMana)), 0);
		}
		return clamp(baseBulk, 0, 600);
	}
	override string pickupmessage(){
		string msg = super.pickupmessage();
		if (forbidden)
			msg.replace(".", ", just holding it makes you shiver.");
		else if (endless)
			msg.replace(".", ", feels heavy!");
		else if (holy)
			msg.replace(".", "! Holding it gives you a bit of excitement.");
		else if (absorberMultiplier > 2)
			msg = msg.." In combination of technology allows the use in the field.";
		else
			msg = msg.." Unlimited power awaits those with the energy within.";
		return msg;
	}
	

	States
	{
		Spawn:
			ARCT Z -1 NoDelay
			{
				if (invoker.ZoomHold) { invoker.ZoomHold = null; }
				if (invoker.InvertSelection) { invoker.InvertSelection = null; }
			}
			Stop;
		ReadyBook:
			ARCB A 1;
			Loop;
		Ready:
			ARCT A 1
			{
				if (!invoker.ZoomHold)
				{
					invoker.ZoomHold = CVar.GetCVar('arcanum_zoomhold', player);
				}
				if (invoker.ZoomHold.GetBool())
				{
					if (PressingZoom() && !(invoker.WeaponStatus[ATProp_Flags] & ATF_InSpellMenu))
					{
						invoker.WeaponStatus[ATProp_Flags] |= ATF_InSpellMenu;
						A_SetHelpText();
					}
					if (!PressingZoom() && invoker.WeaponStatus[ATProp_Flags] & ATF_InSpellMenu)
					{
						invoker.WeaponStatus[ATProp_Flags] &= ~ATF_InSpellMenu;
						A_SetHelpText();
					}
				}
				else if (JustPressed(BT_ZOOM))
				{
					if (invoker.WeaponStatus[ATProp_Flags] & ATF_InSpellMenu)
					{
						invoker.WeaponStatus[ATProp_Flags] &= ~ATF_InSpellMenu;
						A_SetHelpText();
					}
					else
					{
						invoker.WeaponStatus[ATProp_Flags] |= ATF_InSpellMenu;
						A_SetHelpText();
					}
					SetWeaponState("Nope");
					return;
				}

				if (invoker.WeaponStatus[ATProp_Flags] & ATF_InSpellMenu)
				{
					if (!invoker.InvertSelection)
					{
						invoker.InvertSelection = CVar.GetCVar('arcanum_invert', player);
					}

					int InvertFlags = invoker.InvertSelection.GetInt();
					if (JustPressed(BT_ATTACK))
					{
						if (PressingFiremode())
						{
							if (InvertFlags & 1)
							{
								A_PrevTree();
							}
							else
							{
								A_NextTree();
							}
						}
						else if (InvertFlags & 2)
						{
							A_PrevSpell();
						}
						else
						{
							A_NextSpell();
						}
					}
					else if (JustPressed(BT_ALTATTACK))
					{
						if (PressingFiremode())
						{
							if (InvertFlags & 1)
							{
								A_NextTree();
							}
							else
							{
								A_PrevTree();
							}
						}
						else if (InvertFlags & 2)
						{
							A_NextSpell();
						}
						else
						{
							A_PrevSpell();
						}
					}
					A_WeaponReady(WRF_NOFIRE);
					return;
				}
				A_WeaponReady(WRF_ALL);
				
				invoker.SpiedActor = AimTarget();
				if (invoker.SpiedActor && !invoker.SpiedActor.bISMONSTER && !invoker.SpiedActor.player)
				{
					invoker.SpiedActor = null;
				}
			}
			Goto ReadyEnd;
		Select0:
			ARCT A 0 A_Overlay(10, 'ReadyBook');
			Goto Select0Small;
		Deselect0:
			ARCT A 0
			{
				invoker.WeaponStatus[ATProp_Flags] &= ~ATF_InSpellMenu;
			}
			Goto Deselect0Small;
		DeadLowered:
			ARTC A -1 A_ClearOverlays(10, 10);
			Stop;
		User3:
			ARCT A 0 A_SelectWeapon("PickupManager");
			Goto Ready;
		Fire:
			ARCT E 5
			{
				player.GetPSPrite(PSP_WEAPON).frame = random(1, 3);
				FLineTraceData Data;
				for (int i = 8; i <= 192; i += 8)
				{
					//Is this silly? Yes! Does it make casting way more reliable? Also Yes!
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 8, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 7, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 6, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 5, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 4, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 3, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 2, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight + 1, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 0, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 1, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 2, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 3, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 4, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 5, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 6, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 7, 0, 0, Data);
					LineTrace(angle, i, pitch, TRF_NOSKY | TRF_ALLACTORS, player.viewheight - 8, 0, 0, Data);
					if (Data.HitType == Data.TRACE_HitActor && Data.HitActor is 'ArcanumRuneSlot')
					{
						let runeSlot = ArcanumRuneSlot(Data.HitActor);
						let sigil = ArcanumSigil(runeSlot.master);

						if (sigil && sigil.master == self)
						{
							runeSlot.ActivateRune(self);
						}
						break;
					}
				}
			}
			ARCT A 2;
			Goto Nope;
		AltFire:
			ARCT A 1;
			ARCT A 0
			{
				ArcanumSpell SelSpell = invoker.GetSelectedSpell();
				int st = SelSpell.GetSpellType();

				double mPitch = min(ArcanumSigil.SelfCastMaxPitch, player.MaxPitch - (90 - ArcanumSigil.SelfCastMaxPitch));
				if (st & ArcanumSpell.SType_SelfOrMass && !(st & ArcanumSpell.SType_Targeted) && pitch < mPitch)
				{
					A_SetPitch(pitch + 10, SPF_INTERPOLATE);
					A_Refire('AltFire');
					return;
				}

				if (st & ArcanumSpell.SType_SelfOrMass && pitch >= mPitch || st & ArcanumSpell.SType_Targeted)
				{
					SetWeaponState('ActuallyAltFire');
					return;
				}
			}
			Goto Nope;
		ActuallyAltFire:
			ARCT E 5
			{
				ArcanumSpell selSpell = invoker.GetSelectedSpell();

				let plr = HDPlayerPawn(self);
				
				int cost = invoker.GetModifiedRepCost(selSpell);
				double multCube = AceCore.CheckForItem(plr, "HDSoulCube") ? 0.80 : 1.0;
				cost = int(cost * multCube);
				if (invoker.mana >= cost)
				{
					FLineTraceData Data;
					LineTrace(angle, radius + 10, pitch, TRF_NOSKY, height - 6, 0, 0, Data);
					if (Data.HitType == Data.TRACE_HitNone || Data.HitType == Data.TRACE_HitFloor)
					{
						invoker.mana -= cost;

						ArcanumSigil s = ArcanumSigil(Spawn(selSpell.GetSigil(), Data.HitLocation));
						s.angle = angle;
						if (!s.bDONTPITCH)
						{
							s.pitch = pitch;
						}
						s.master = self;
						s.ExperienceFactor = selSpell.GetExperienceFactor();
						s.Spell = selSpell;

						double mPitch = min(ArcanumSigil.SelfCastMaxPitch, player.MaxPitch - (90 - ArcanumSigil.SelfCastMaxPitch));
						int st = selSpell.GetSpellType();
						if (st & ArcanumSpell.SType_SelfOrMass && pitch >= mPitch)
						{
							s.SigilType = ArcanumSpell.SType_SelfOrMass;
						}
						else
						{
							s.SigilType = ArcanumSpell.SType_Targeted;
						}
					}
				}
				else
				{
					if (invoker.GetRealMaxAmount() < cost)
						A_WeaponMessage("You will never reach the ability to cast "..selSpell.GetName()..".");
					else
						A_WeaponMessage("You don't feel powerful enough to cast "..selSpell.GetName()..".");
				}
			}
			ARCT A 2;
			Goto Nope;
	}
}
