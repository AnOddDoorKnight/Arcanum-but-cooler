class ConvalescenceSpell : ArcanumSpell
{
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeRestoration'; }
	override int GetIndex() { return 0; }
	override string GetName() { return "Convalescence"; }
	override int GetSpellType() { return SType_SelfOrMass | SType_Targeted; }
	override int GetCatalystAmount() { return 75; }
	override string GetDescription() { return String.Format("Improves health regeneration for \c[Gold]%i\c- seconds.", 60 * Level); }
	override string GetTechnicalInfo() { return "Self, Targeted, Touch"; }
	override int GetExperienceGain() { return 2; }
	override int GetCost() { return 20; }
	override int GetAggroGain() { return 2; }
	override class<ArcanumSigil> GetSigil() { return 'ConvalescenceSigil'; }
}

class ConvalescenceSigil : ArcanumSigil
{
	override void ActivateTargeted()
	{
		let plr = HDPlayerPawn(AimTarget());
		if (plr && plr.Health > 0 && Distance3D(plr) <= 42 * 2)
		{
			DoHealing(plr);
		}
	}

	override void ActivateSelfOrMass()
	{
		let plr = HDPlayerPawn(master);
		if (plr)
		{
			DoHealing(plr);
		}
	}

	private void DoHealing(HDPlayerPawn plr)
	{
		PlayCastSound(self, "Arcanum/Restoration/Convalescence");
		plr.A_GiveInventory("ConvalescenceEffectL"..SigilLevel);
	}

	override void InitRuneSlots()
	{
		vector2 Size = (4, 6.66);
		CreateRuneSlot((0, 13.4), Size, 1.0, 2.5);
		CreateRuneSlot((0, -13.4), Size, 1.0, 2.5);
		CreateRuneSlot((-15, 0), Size, 1.0, 2.5);
		CreateRuneSlot((15, 0), Size, 1.0, 2.5);
	}

	Default
	{
		ArcanumSigil.FadeOutSpeedMult 2.0;
		StencilColor "FF9E9E";
	}

	States
	{
		Spawn:
			CRRE A 0;
			Goto Super::Spawn;
	}
}

class ConvalescenceEffectL1 : Powerup
{
	override void DoEffect()
	{
		let plr = HDPlayerPawn(owner);
		if (plr && plr.beatcount == 0 && plr.beatcounter % 4 == 0)
		{
			plr.GiveBody(1);
		}

		Super.DoEffect();
	}

	Default
	{
		Powerup.Duration -60;
	}
}

class ConvalescenceEffectL2 : ConvalescenceEffectL1 { Default { Powerup.Duration -120; } }
class ConvalescenceEffectL3 : ConvalescenceEffectL1 { Default { Powerup.Duration -180; } }