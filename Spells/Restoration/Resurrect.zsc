class ResurrectSpell : ArcanumSpell
{
	override int GetIndex() { return 30; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeRestoration'; }
	override string GetName() { return "Resurrect"; }
	override int GetSpellType() { return SType_Targeted; }
	override int GetCatalystAmount() { return 600; }
	override string GetDescription() { return String.Format("Resurrects up to \c[Gold]%i\c- targets within \c[Gold]%i\c-m of where the rune's touch effect hits.", 10 * Level, 2 * Level); }
	override string GetTechnicalInfo() { return "Targeted, Touch"; }
	override int GetExperienceGain() { return 3; }
	override int GetCost() { return 80; }
	override int GetAggroGain() { return 10; }
	override class<ArcanumSigil> GetSigil() { return 'ResurrectSigil'; }
}

class ResurrectSigil : ArcanumSigil
{
	override void ActivateTargeted()
	{
		FLineTraceData Data;
		LineTrace(angle, 42 * 2, pitch, 0, 0, 0, 0, Data);

		HDMobBase Enemy;
		HDPlayerCorpse PCorpse;

		bool HasPlayedSound;
		int RevivesLeft = 10 * SigilLevel;
		vector3 AdjustedLocation = Data.HitLocation - Data.HitDir;
		BlockThingsIterator it = BlockThingsIterator.CreateFromPos(AdjustedLocation.x, AdjustedLocation.y, AdjustedLocation.z, 0, 84 * SigilLevel, false);
		while (RevivesLeft > 0 && it.Next())
		{
			if (!it.thing.bCORPSE)
			{
				continue;
			}

			if (!HasPlayedSound)
			{
				PlayCastSound(self, "Arcanum/Restoration/Resurrect");
				HasPlayedSound = true;
			}

			Enemy = HDMobBase(it.thing);
			PCorpse = HDPlayerCorpse(it.thing);

			if (PCorpse && HDPlayerPawn(PCorpse.master))
			{
				HDPlayerPawn(PCorpse.master).respawndelay = 0;
				RevivesLeft--;
			}
			else if (Enemy)
			{
				Enemy.bodydamage = 0;
				master.RaiseActor(Enemy, RF_NOCHECKPOSITION);
				Enemy.bFRIENDLY = true;
				RevivesLeft--;
			}
		}
	}

	override void InitRuneSlots()
	{
		vector2 Size = (4, 6.666);
		CreateRuneSlot((0, 0), (4, 6.66), 1.15, 1.20);
		CreateRuneSlot((0, 20), Size, 1.0, 1.20);
		CreateRuneSlot((-19.6, 5.9), Size, 1.0, 1.20); CreateRuneSlot((19.6, 5.9), Size, 1.0, 1.20);
		CreateRuneSlot((-12, -16.9), Size, 1.0, 1.20); CreateRuneSlot((12, -16.9), Size, 1.0, 1.20);
	}

	Default
	{
		ArcanumSigil.AddAlpha 1.75;
		StencilColor "331177";
	}

	States
	{
		Spawn:
			CRRE D 0;
			Goto Super::Spawn;
	}
}