class StoneFleshSpell : ArcanumSpell
{
	override int GetIndex() { return 80; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeAlteration'; }
	override string GetName() { return "StoneFlesh"; }
	override int GetSpellType() { return SType_SelfOrMass; }
	override int GetCatalystAmount() { return 800; }
	override string GetDescription() { return String.Format("All visible players within 20m get 50%% increased resistance to most damage for \c[Gold]%i\c- minutes. Effect helps put out fire faster.", 2 + 1 * Level); }
	override string GetTechnicalInfo() { return "Mass"; }
	override int GetExperienceGain() { return 4; }
	override int GetCost() { return 175; }
	override int GetAggroGain() { return 15; }
	override class<ArcanumSigil> GetSigil() { return 'StoneFleshSigil'; }
}

class StoneFleshSigil : ArcanumSigil
{
	override void ActivateSelfOrMass()
	{
		PlayCastSound(self, "Arcanum/Alteration/StoneFlesh");
		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			let plr = players[i].mo;
			if (plr && Distance3D(plr) <= 42 * 20 && CheckSight(plr))
			{
				for (int i = 0; i < SigilLevel; ++i)
				{
					plr.A_GiveInventory("StoneFleshEffect");
					let Effect = StoneFleshEffect(plr.FindInventory("StoneFleshEffect"));
					Effect.Level = SigilLevel;
				}
			}
		}
	}

	override void InitRuneSlots()
	{
		vector2 BigSize = (4, 6.66);

		vector2 Size = (3, 5);
		double RSize = 0.75;
		double X = 3.3, Y = 15.5;
		CreateRuneSlot((-X, Y), Size, RSize);
		CreateRuneSlot((X, Y), Size, RSize);
		CreateRuneSlot((Y, -X), Size, RSize);
		CreateRuneSlot((Y, X), Size, RSize);
		CreateRuneSlot((X, -Y), Size, RSize);
		CreateRuneSlot((-X, -Y), Size, RSize);
		CreateRuneSlot((-Y, -X), Size, RSize);
		CreateRuneSlot((-Y, X), Size, RSize);

		Size = (4, 6.666);
		RSize = 1.0;
		X = 16.75; Y = 16.75;
		CreateRuneSlot((X, Y), Size, RSize);
		CreateRuneSlot((X, -Y), Size, RSize);
		CreateRuneSlot((-X, -Y), Size, RSize);
		CreateRuneSlot((-X, Y), Size, RSize);
	}

	Default
	{
		Height 44;
		Radius 50;
		StencilColor "B4A888";
	}

	States
	{
		Spawn:
			CRAL I 0;
			Goto Super::Spawn;
	}
}

class StoneFleshEffect : Powerup
{
	override void ModifyDamage(int damage, Name damageType, out int newdamage, bool passive, Actor inflictor, Actor source, int flags)
	{
		if (passive && !(flags & (DMG_NO_FACTOR | DMG_FORCED)) && damageType != 'bleedout' && damageType != 'thermal' && damageType != 'maxhpdrain' && damageType != 'internal' && damageType != 'falling' && damageType != 'holy')
		{
			newdamage = int(damage * 0.50);
		}
	}

	override void DoEffect()
	{
		EffectTics = min(EffectTics, 35 * 60 * (2 + 1 * Level));
		if (owner)
		{
			owner.A_GiveInventory("HDFireDouse", 20);
		}

		Super.DoEffect();
	}

	int Level;

	Default
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.ADDITIVETIME
		Powerup.Duration 0xFFFF;
	}
}