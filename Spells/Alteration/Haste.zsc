class HasteSpell : ArcanumSpell
{
	override int GetIndex() { return 30; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeAlteration'; }
	override string GetName() { return "Haste"; }
	override int GetSpellType() { return SType_SelfOrMass; }
	override string GetDescription() { return String.Format("All visible players within 20m get an increase to speed by \c[Gold]%i\c-%% for \c[Gold]%i\c- minutes.", 50 + 25 * Level, 2 + 2 * Level); }
	override string GetTechnicalInfo() { return "Mass"; }
	override int GetExperienceGain() { return 4; }
	override int GetCost() { return 30; }
	override int GetAggroGain() { return 10; }
	override class<ArcanumSigil> GetSigil() { return 'HasteSigil'; }
}

class HasteSigil : ArcanumSigil
{
	override void ActivateSelfOrMass()
	{
		PlayCastSound(self, "Arcanum/Alteration/Haste");
		for (int i = 0; i < MAXPLAYERS; ++i)
		{
			let plr = players[i].mo;
			if (plr && Distance3D(plr) <= 42 * 20 && CheckSight(plr))
			{
				for (int i = 0; i < SigilLevel; ++i)
				{
					plr.A_GiveInventory("HasteEffect");
					let Effect = HasteEffect(plr.FindInventory("HasteEffect"));
					Effect.Level = SigilLevel;
				}
			}
		}
	}

	override void InitRuneSlots()
	{
		vector2 Size = (4, 6.666);
		double A = 18.3;
		double B = 12.94;
		double RSize = 1.25;
		double FSpeed = 1.5;
		CreateRuneSlot((0, A), Size, RSize, FSpeed);
		CreateRuneSlot((B, B), Size, RSize, FSpeed);
		CreateRuneSlot((A, 0), Size, RSize, FSpeed);
		CreateRuneSlot((B, -B), Size, RSize, FSpeed);
		CreateRuneSlot((0, -A), Size, RSize, FSpeed);
		CreateRuneSlot((-B, -B), Size, RSize, FSpeed);
		CreateRuneSlot((-A, 0), Size, RSize, FSpeed);
		CreateRuneSlot((-B, B), Size, RSize, FSpeed);
	}

	Default
	{
		Height 44;
		Radius 50;
		StencilColor "FFFF11";
	}

	States
	{
		Spawn:
			CRAL D 0;
			Goto Super::Spawn;
	}
}

class HasteEffect : PowerSpeed
{
	override void DoEffect()
	{
		EffectTics = min(EffectTics, 35 * 60 * (2 + 2 * Level));
		Speed = 1.50 + 0.25 * Level;
		Super.DoEffect();
	}

	int Level;

	Default
	{
		Speed 1.75;
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.ADDITIVETIME
		Powerup.Duration 0xFFFF;
	}
}