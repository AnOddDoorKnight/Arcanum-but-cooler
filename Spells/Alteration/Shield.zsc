class ShieldSpell : ArcanumSpell
{
	const ShieldPerLevel = 750;
	override int GetIndex() { return 20; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeAlteration'; }
	override string GetName() { return "Shield"; }
	override int GetSpellType() { return SType_SelfOrMass | SType_Targeted; }
	override int GetCatalystAmount() { return 500; }
	override string GetDescription() { return String.Format("Target is surrounded with a shield that can absorb up to \c[Gold]%i\c- damage.", ShieldPerLevel * Level); }
	override string GetTechnicalInfo() { return "Self, Targeted, Touch"; }
	override int GetExperienceGain() { return 3; }
	override int GetCost() { return 70; }
	override int GetAggroGain() { return 15; }
	override class<ArcanumSigil> GetSigil() { return 'ShieldSigil'; }
	override void DrawHudStuff(HDStatusBar sb, HDPlayerPawn hpl)
	{
		let Shield = hpl.FindInventory("HDMagicShield");
		if (Shield && Shield.Amount > 0)
		{
			sb.DrawRect(-33, -22, 2, 8);
			sb.DrawRect(-31, -20, 2, 8);
			sb.DrawRect(-29, -22, 2, 12);
			sb.DrawRect(-27, -20, 2, 8);
			sb.DrawRect(-25, -22, 2, 8);
			sb.DrawWepNum(Shield.Amount, ShieldPerLevel * Level);
		}
	}
}

class ShieldSigil : ArcanumSigil
{
	override void ActivateTargeted()
	{
		let trgt = AimTarget();
		if (trgt && trgt.Health > 0 && (trgt.bISMONSTER || trgt.player) && Distance3D(trgt) <= 42 * 2)
		{
			CastShield(trgt);
		}
	}

	override void ActivateSelfOrMass()
	{
		if (master)
		{
			CastShield(master);
		}
	}

	private void CastShield(Actor trgt)
	{
		trgt.A_StartSound("misc/mobshieldf", CHAN_BODY, CHANF_OVERLAP, 0.75);
		for (int i = 0; i < 20; ++i)
		{
			vector3 RandPos = trgt.pos + (frandom(-trgt.radius, trgt.radius), frandom(-trgt.radius, trgt.radius), frandom(0, trgt.height));
			actor Spark = Spawn("ShieldSpark", RandPos, ALLOW_REPLACE);
			vector3 sv = Spark.Vec3To(trgt);
			sv.z += trgt.height / 2;
			Spark.vel = (sv / 50.0);
		}
		trgt.A_GiveInventory("HDMagicShield");
		let Shield = HDMagicShield(trgt.FindInventory("HDMagicShield"));
		Shield.MaxAmount = 0;
		Shield.Amount = ShieldSpell.ShieldPerLevel * SigilLevel;
		Shield.bQUICKTORETALIATE = false;
		Shield.bSTANDSTILL = true;
	}

	override void InitRuneSlots()
	{
		vector2 Size = (2.2, 3.666);
		double X = 6.3, Y = 6.3;

		CreateRuneSlot((X, Y), Size, 0.5, 1.0);
		CreateRuneSlot((X, -Y), Size, 0.5, 1.0);
		CreateRuneSlot((-X, -Y), Size, 0.5, 1.0);
		CreateRuneSlot((-X, Y), Size, 0.5, 1.0);

		Size = (3.5, 5.833);
		X = 17; Y = 17;
		CreateRuneSlot((0, Y), Size, 0.9, 1.0);
		CreateRuneSlot((X, 0), Size, 0.9, 1.0);
		CreateRuneSlot((0, -Y), Size, 0.9, 1.0);
		CreateRuneSlot((-X, 0), Size, 0.9, 1.0);
	}

	Default
	{
		ArcanumSigil.FadeOutSpeedMult 3.0;
		StencilColor "5CFA6E";
	}

	States
	{
		Spawn:
			CRAL C 0;
			Goto Super::Spawn;
	}
}