class ShieldSpell : ArcanumSpell
{
	static const int Shields[] = { 1000, 2500, 7500 };
	override int GetIndex() { return 20; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeAlteration'; }
	override string GetName() { return "Shield"; }
	override int GetSpellType() { return SType_SelfOrMass | SType_Targeted; }
	override int, int GetCastRequirements() { return 400, HDBLU_BOTTLE * HDBLU_MOUTH / 2; }
	override string GetDescription() { return String.Format("Target is surrounded with a shield that can absorb up to \c[Gold]%i\c- damage.", Shields[Level - 1]); }
	override string GetTechnicalInfo() { return "Self, Targeted, Touch"; }
	override int GetExperienceGain() { return 3; }
	override class<ArcanumSigil> GetSigil() { return 'ShieldSigil'; }
	override void DrawHudStuff(HDStatusBar sb, HDPlayerPawn hpl)
	{
		let Shield = hpl.FindInventory("ArcanumShield");
		if (Shield && Shield.Amount > 0)
		{
			sb.DrawRect(-33, -22, 2, 8);
			sb.DrawRect(-31, -20, 2, 8);
			sb.DrawRect(-29, -22, 2, 12);
			sb.DrawRect(-27, -20, 2, 8);
			sb.DrawRect(-25, -22, 2, 8);
			sb.DrawWepNum(Shield.Amount, Shields[Level - 1]);
		}
	}
}

class ShieldSigil : ArcanumSigil
{
	override void ActivateTargeted()
	{
		let trgt = AimTarget();
		if (trgt && trgt.Health > 0 && (trgt.bISMONSTER || trgt.player) && Distance3D(trgt) <= HDCONST_ONEMETRE * 2)
		{
			CastShield(trgt);
		}
	}

	override void ActivateSelfOrMass()
	{
		if (master)
		{
			CastShield(master);
		}
	}

	private void CastShield(Actor trgt)
	{
		trgt.A_StartSound("misc/mobshieldf", CHAN_BODY, CHANF_OVERLAP, 0.75);
		for (int i = 0; i < 20; ++i)
		{
			vector3 RandPos = trgt.pos + (frandom(-trgt.radius, trgt.radius), frandom(-trgt.radius, trgt.radius), frandom(0, trgt.height));
			actor Spark = Spawn("ShieldSpark", RandPos, ALLOW_REPLACE);
			vector3 sv = Spark.Vec3To(trgt);
			sv.z += trgt.height / 2;
			Spark.vel = (sv / 50.0);
		}
		trgt.A_GiveInventory("ArcanumShield");
		let Shield = ArcanumShield(trgt.FindInventory("ArcanumShield"));
		Shield.MaxAmount = 0; // [Ace] This is so that the shield gets destroyed when it depletes.
		Shield.Stamina = ShieldSpell.Shields[SigilLevel - 1] * 2;
		Shield.Amount = ShieldSpell.Shields[SigilLevel - 1];
	}

	override void InitRuneSlots()
	{
		vector2 Size = (2.2, 3.666);
		double X = 6.3, Y = 6.3;

		CreateRuneSlot((X, Y), Size, 0.5);
		CreateRuneSlot((X, -Y), Size, 0.5);
		CreateRuneSlot((-X, -Y), Size, 0.5);
		CreateRuneSlot((-X, Y), Size, 0.5);

		Size = (3.5, 5.833);
		X = 17; Y = 17;
		CreateRuneSlot((0, Y), Size, 0.9);
		CreateRuneSlot((X, 0), Size, 0.9);
		CreateRuneSlot((0, -Y), Size, 0.9);
		CreateRuneSlot((-X, 0), Size, 0.9);
	}

	Default
	{
		ArcanumSigil.FadeOutSpeedMult 3.0;
		StencilColor "5CFA6E";
	}

	States
	{
		Spawn:
			CRAL C 0;
			Goto Super::Spawn;
	}
}

class ArcanumShield : HDMagicShield
{
	Default
	{
		Inventory.MaxAmount 1000;
		-QUICKTORETALIATE
		+STANDSTILL
		+INVENTORY.KEEPDEPLETED
		-INVENTORY.INVBAR
		HDDamageHandler.Priority 10001; // [Ace] Handle it before the regular shield.
	}
}